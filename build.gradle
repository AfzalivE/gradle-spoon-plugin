plugins {
  id "com.gradle.build-scan" version "1.10.2"
  id "com.github.kt3k.coveralls" version "2.8.2"
  id "com.jfrog.artifactory" version "4.4.18"
  id "com.jfrog.bintray" version "1.8.0"
  id "com.github.ben-manes.versions" version "0.17.0"
  id "com.gradle.plugin-publish" version "0.9.9"
  id "org.jetbrains.kotlin.jvm" version "1.1.60"
  id "java-gradle-plugin"
  id "groovy"
  id "maven-publish"
  id "jacoco"
}

repositories {
  jcenter()
  google()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // For Spoon snapshot, until 2.0.0 is released
}

apply from: file("gradle/dependencies.gradle")

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = ext.javaVersion
targetCompatibility = ext.javaVersion

dependencies {
  implementation deps.gradle
  implementation deps.spoonRunner
  implementation deps.kotlinStdlib

  testImplementation localGroovy()
  testImplementation deps.spockCore, { exclude module: "groovy-all" } // Use localGroovy()
}

// Ordering matters
apply from: file("gradle/scan.gradle")
apply from: file("gradle/compile.gradle")
apply from: file("gradle/plugin.gradle")
apply from: file("gradle/publish.gradle")
apply from: file("gradle/quality.gradle")
apply from: file("gradle/wrapper.gradle")

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  kotlinOptions {
    jvmTarget = rootProject.ext.javaVersion
  }
}
